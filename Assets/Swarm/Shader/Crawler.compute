// Swarm - Special renderer that draws a swarm of wobbling/crawling tubes.
// https://github.com/keijiro/Swarm

#pragma kernel CrawlerInit
#pragma kernel CrawlerUpdate
#pragma kernel CrawlerReconstruct

#include "UnityCG.cginc"
#include "SimplexNoise3D.cginc"

RWStructuredBuffer<float4> PositionBuffer;
StructuredBuffer<float4> PositionBufferRO;

RWStructuredBuffer<float4> TangentBuffer;
RWStructuredBuffer<float4> NormalBuffer;

Texture3D<float4> DFVolume;
SamplerState samplerDFVolume;

CBUFFER_START(Params)
    uint InstanceCount;
    uint HistoryLength;
    uint IndexOffset0;
    uint IndexOffset1;
    uint IndexOffset2;
    float Time;
    float Speed;
    float Constraint;
    float NoiseFrequency;
    float NoiseOffset;
CBUFFER_END

float UVRandom(float u, float v)
{
    float f = dot(float2(12.9898, 78.233), float2(u, v));
    return frac(43758.5453 * sin(f));
}

float4 SampleVolume(float3 p)
{
    return DFVolume.SampleLevel(samplerDFVolume, p * 0.5 + 0.5, 0);
}

[numthreads(64, 1, 1)]
void CrawlerInit(uint id : SV_DispatchThreadID)
{
    // Get a random point.
    float x = UVRandom(id * 0.01334, 0.3728);
    float y = UVRandom(0.8372, id * 0.01197);
    float z = UVRandom(4.438, id * 0.01938 - 4.378);
    float4 p = float4(x, y, z, 0) * 0.5 - 0.25;
    float d = SampleVolume(p);

    // Swizzle and compre, choose one with closer distance.
    {
        float4 p2 = p.yzxw;
        float d2 = SampleVolume(p2);
        p = d2 < d ? p2 : p;
        d = min(d, d2);
    }

    // Swizzle again.
    {
        float4 p2 = p.zxyw;
        float d2 = SampleVolume(p2);
        p = d2 < d ? p2 : p;
        d = min(d, d2);
    }

    // Fill the position/tangent/normal buffer.
    for (uint i = 0; i < HistoryLength; i++)
    {
        uint offs = id + i * InstanceCount;
        PositionBuffer[offs] = p;
        TangentBuffer[offs] = float4(0, 0, 1, 0);
        NormalBuffer[offs] = float4(1, 0, 0, 0);
    }
}

[numthreads(64, 1, 1)]
void CrawlerUpdate(uint id : SV_DispatchThreadID)
{
    // Retrieve the previous position.
    float3 p = PositionBuffer[IndexOffset1 + id].xyz;

    // Distance field (gradient.x, y, z, distance).
    float4 df = SampleVolume(p) * Constraint;

    // Two independent noise fields (not completely independent though).
    float4 sn1 = snoise(10.33 + p.xyz * NoiseFrequency + NoiseOffset);
    float4 sn2 = snoise(32.38 - p.yzx * NoiseFrequency + NoiseOffset);

    // Apply the boundary condition to the first noise field.
    sn1.xyz = -sn1.xyz * df.w + df.xyz * sn1.w;

    // Apply the divergence free noise field to the position.
    p += cross(sn1.xyz, sn2.xyz) * Speed;

    // Update the buffer.
    PositionBuffer[IndexOffset2 + id].xyz = p;
}

[numthreads(64, 1, 1)]
void CrawlerReconstruct(uint id : SV_DispatchThreadID)
{
    // Retrieve the history.
    float3 p0 = PositionBufferRO[IndexOffset0 + id].xyz; // Two frames ago
    float3 p1 = PositionBufferRO[IndexOffset1 + id].xyz; // Previous frame
    float3 p2 = PositionBufferRO[IndexOffset2 + id].xyz; // Current frame

    float3 t0 = TangentBuffer[IndexOffset0 + id].xyz; // Two frames ago
    float3 n0 = NormalBuffer[IndexOffset0 + id].xyz;
    float3 b0 = cross(t0, n0);

    // Tangent vectors.
    float3 t1 = normalize(p2 - p0);
    float3 t2 = normalize(p2 - p1);

    // Compute normal vectors based on b0.
    float3 n1 = normalize(cross(b0, t1));
    float3 b1 = cross(t1, n1);
    float3 n2 = normalize(cross(b1, t2));

    // Store the results.
    TangentBuffer[IndexOffset1 + id] = float4(t1, 0);
    TangentBuffer[IndexOffset2 + id] = float4(t2, 0);
    NormalBuffer[IndexOffset1 + id] = float4(n1, 0);
    NormalBuffer[IndexOffset2 + id] = float4(n2, 0);
}
